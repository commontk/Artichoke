#
# @PROJECT_NAME_CONFIG@
#

set(proj @PROJECT_NAME_CONFIG@)
set(depends "@PROJECT_DEPENDS_CONFIG@")

ExternalProject_Include_Dependencies(${proj}
  PROJECT_VAR proj
  DEPENDS_VAR depends
  EP_ARGS_VAR ep_args
  USE_SYSTEM_VAR ${CMAKE_PROJECT_NAME}_USE_SYSTEM_${proj}
  # Can optionally be set in top-level CMakeLists.txt before
  # calling "configure_external_projects_for_test()"
  @LIB_EXTERNAL_PROJECT_INCLUDE_DEPENDENCIES_EXTRA_ARGS@
  )

include(${CMAKE_CURRENT_SOURCE_DIR}/../ArtichokeTestUtility.cmake)
check_variable(proj "@PROJECT_NAME_CONFIG@")
check_variable(depends "@PROJECT_REQUIRED_DEPENDS_CONFIG@")
check_variable(${CMAKE_PROJECT_NAME}_USE_SYSTEM_${proj} "")

if(${CMAKE_PROJECT_NAME}_USE_SYSTEM_${proj})
  message(FATAL_ERROR "Enabling ${CMAKE_PROJECT_NAME}_USE_SYSTEM_${proj} is not supported !")
endif()

# Sanity checks
if(DEFINED @PROJECT_NAME_CONFIG@_DIR AND NOT EXISTS ${@PROJECT_NAME_CONFIG@_DIR})
  message(FATAL_ERROR "@PROJECT_NAME_CONFIG@_DIR variable is defined but corresponds to non-existing directory")
endif()

check_for_uses_terminal(${proj} ep_args)

if(NOT DEFINED @PROJECT_NAME_CONFIG@_DIR AND NOT ${CMAKE_PROJECT_NAME}_USE_SYSTEM_${proj})

  ExternalProject_Add(${proj}
    ${ep_args}
    SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/Lib
    BINARY_DIR ${proj}-build
    DOWNLOAD_COMMAND ""
    BUILD_COMMAND ""
    INSTALL_COMMAND ""
    DEPENDS ${depends}
    )
  set(@PROJECT_NAME_CONFIG@_DIR ${CMAKE_BINARY_DIR}/${proj}-build)

else()
  ExternalProject_Add_Empty(${proj} DEPENDS "${depends}")
endif()

set(@PROJECT_NAME_CONFIG@_EP_ADDED 1)

mark_as_superbuild(
  VARS @PROJECT_NAME_CONFIG@_DIR:PATH
  LABELS "FIND_PACKAGE"
  )
